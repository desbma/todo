# Maintainer: desbma
# shellcheck disable=SC2034,SC2148,SC2154,SC2164
pkgname=todo
pkgver=2025.6.8
pkgrel=2
pkgdesc='Todo.txt terminal frontend'
arch=('x86_64')
url="https://github.com/desbma/${pkgname}"
license=('GPL')
depends=('gcc-libs' 'zstd')
makedepends=('cargo' 'git')
source=("git+https://github.com/desbma/${pkgname}")
sha512sums=('SKIP')

pkgver() {
    date +%Y.%-m.%-d
}

prepare() {
    cd "${pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked
}

build() {
    cd "${pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo build --frozen --release
}

check() {
    cd "${pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen
}

package() {
    cd "${pkgname}"
    install -Dm 755 -t "${pkgdir}/usr/bin" ./target/release/${pkgname}
}
